buildscript {
    repositories {
        mavenCentral()
    }
}

plugins {
    // Apply the java-library plugin to add support for Java Library
    id 'java-library'
    id 'jacoco'
    id 'com.github.spotbugs'
    id "org.sonarqube" version "3.3"
    id "pl.allegro.tech.build.axion-release" version "1.13.3"
}

scmVersion {
    tag {
        prefix = 'javafp-'
    }
    versionIncrementer 'incrementPatch'
}

project.version = scmVersion.version

repositories {
    mavenCentral()
}

sonarqube {
    properties {
        property "sonar.projectKey", "Tyagi-Gaurav_javafp"
        property "sonar.organization", "tyagi-gaurav"
        property "sonar.host.url", "https://sonarcloud.io"
        property "sonar.java.coveragePlugin", "jacoco"
        property "sonar.coverage.jacoco.xmlReportPaths", "build/customJacocoReportDir/test/jacocoTestReport.xml"
    }
}

jacoco {
    toolVersion = "0.8.6"
    reportsDirectory = layout.buildDirectory.dir('customJacocoReportDir')
}

spotbugs {
    toolVersion = '4.2.3'
    ignoreFailures = false
    excludeFilter = file("gradle/spotbugs/exclusions.xml")
}

spotbugsMain {
    reports {
        html {
            enabled = true
            destination = file("$buildDir/reports/spotbugs/main/spotbugs.html")
            stylesheet = 'fancy-hist.xsl'
        }
    }
}

spotbugsTest {
    reports {
        html {
            enabled = true
            destination = file("$buildDir/reports/spotbugs/test/spotbugs.html")
            stylesheet = 'fancy-hist.xsl'
        }
    }
}

dependencies {
    testImplementation "org.junit.jupiter:junit-jupiter-api:5.7.1"
    testImplementation "org.junit.jupiter:junit-jupiter-engine:5.7.1"
    testImplementation "org.junit.jupiter:junit-jupiter-params:5.7.1"
    testImplementation "org.junit.platform:junit-platform-commons:1.7.1"
    testImplementation "org.junit.platform:junit-platform-runner:1.7.1"
    testImplementation "org.junit.platform:junit-platform-suite-api:1.7.1"
    testImplementation "org.apache.commons:commons-lang3:3.12.0"

    testImplementation "org.assertj:assertj-core:3.15.0"
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    dependsOn test

    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: [
                    "com/gt/jfp/construct/domain/Triple.class",
                    "com/gt/jfp/construct/domain/Tuple.class",
            ])
        }))
    }

    reports {
        xml.enabled true
        csv.enabled false
        html.destination layout.buildDirectory.dir('jacocoHtml').get().asFile
    }
}